import java.text.SimpleDateFormat

apply plugin: 'idea'
//and standard one
apply plugin: 'java'
apply plugin: 'application'

group = "com.github.dzmipt"
archivesBaseName = "kdbStudio"
version = "dz1.10"

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

configurations {
    txtmark
    intTestImplementation.extendsFrom implementation
    intTestRunimeOnly.extendsFrom runtimeOnly
}

sourceSets {
    main {
        java {
            srcDirs 'src'
        }
        resources {
            srcDirs 'src','images'
            include 'log4j2.xml'
            include 'studio*.properties'
            include 'org/netbeans/editor/Bundle*.properties'
            include '*.png'
            include 'notes.html'
            include 'build.txt'
        }
    }
    test {
        java {
            srcDirs 'test'
        }
        resources {
            srcDirs 'test'
            include 'syntax.csv'
        }
    }

    intTest {
        java.srcDirs += ['test-integration']
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

dependencies {
    txtmark group: 'com.github.rjeschke', name: 'txtmark', version: '0.13'

    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-iostreams', version: '2.14.0'
    compile group: 'org.netbeans.api', name: 'org-openide-awt', version: 'RELEASE113'
    compile group: 'org.jfree', name: 'jfreechart', version: '1.5.3'
    runtime files ('lib/images.jar')

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.3.1'
    intTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    intTestImplementation 'org.junit.jupiter:junit-jupiter-params:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

test {
    useJUnitPlatform()
    systemProperty 'java.awt.headless', 'true'
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
}

application {
    mainClassName = 'studio.core.Studio'
}

def buildTime() {
    def df = new SimpleDateFormat("yyyy.MM.dd HH:mm:ss")
    return df.format(new Date())
}

task getBuildHash {
    doLast {
        file("src/build.txt").text = buildTime()
    }
}

task convertNotes (type: JavaExec) {
    main = 'com.github.rjeschke.txtmark.Run'
    classpath = configurations.txtmark
    args 'notes.md'
    standardOutput new FileOutputStream(new File(projectDir,"src/notes.html"))
}

processResources.dependsOn('getBuildHash', 'convertNotes')

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
    failOnError = false
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar, sourcesJar
}
